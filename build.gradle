plugins {
    id "com.github.maiflai.scalatest" version "0.10"
}

apply plugin: "scala"
apply plugin: "application"

group = group
version = version

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime

    testCompile.exclude module: "slf4j-log4j12"
}

sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        scala.srcDir file('src/itest/scala')
    }

    test {
        resources.srcDir file('src/itest/resources')
    }
}

task itest(type: Test) {
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
    }
}


repositories {
    mavenCentral()
}

dependencies {
    def scalaVersion = "2.10"
    def akkaVersion = "2.3.14"

    compile "org.scala-lang:scala-library:$scalaVersion.6"
    compile "com.typesafe.akka:akka-actor_$scalaVersion:$akkaVersion"
    compile "com.typesafe.akka:akka-cluster_$scalaVersion:$akkaVersion"
    compile "com.typesafe.akka:akka-slf4j_$scalaVersion:$akkaVersion"
    compile "com.softwaremill.reactivekafka:reactive-kafka-core_$scalaVersion:0.8.3"
    compile "com.softwaremill.reactivekafka:zookeeper-committer_$scalaVersion:0.8.3"
    compile "com.datastax.cassandra:cassandra-driver-core:3.0.0-alpha5"
    compile "ch.qos.logback:logback-classic:1.1.3"

    testCompile "com.typesafe.akka:akka-testkit_$scalaVersion:$akkaVersion"
    testCompile "org.scalatest:scalatest_$scalaVersion:2.2.4"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.apache.zookeeper:zookeeper:3.4.7"
    testCompile "net.manub:scalatest-embedded-kafka_$scalaVersion:0.4.1"
    testRuntime "org.pegdown:pegdown:1.1.0"
}

mainClassName = "com.pygmalios.rawKafkaCassandra.RawKafkaCassandraApp"